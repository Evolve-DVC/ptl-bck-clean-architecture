plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${dependencyManagementPlugin}"
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}

dependencies {
    // Project Dependencies
    implementation project(':commons')
    implementation project(':domain')

    //Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation "org.springframework.kafka:spring-kafka:${kafkaVersion}" // Spring Kafka

    // Spring Boot Configuration Processor (genera metadata para autocompletado del IDE)
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // Spring Statemachine
    implementation "org.springframework.statemachine:spring-statemachine-core:${springStatemachineVersion}" // Versión gestionada por el BOM

    // Database
    runtimeOnly "com.oracle.database.jdbc:ojdbc8:${oraclejdbcVersion}" // Oracle
    implementation "org.postgresql:postgresql:${postgresqlVersion}" // Postgres

    // gRPC
    implementation "io.grpc:grpc-protobuf:${iogrpcVersion}"
    implementation "io.grpc:grpc-stub:${iogrpcVersion}"
    implementation "io.grpc:grpc-netty:${iogrpcVersion}"
    implementation "net.devh:grpc-spring-boot-starter:${grpcSpringBootStarter}"

    // Code Generation & Utilities - ORDEN CRÍTICO: Lombok primero, MapStruct segundo, binding al final
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    // API Documentation
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocOpenapiVersion}" // Swagger

    // Testing
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"

    // Other Utilities
    implementation "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}" // JavaX Annotation
    implementation "commons-beanutils:commons-beanutils:${commonsBeanutilsVersion}" // Commons BeanUtils
    implementation "io.github.classgraph:classgraph:${ioGithubClassgraph}" // Classpath and module scanner
    implementation "org.apache.poi:poi-ooxml:${apachePoiVersion}" // Apache POI for Excel
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${iogrpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDirs 'src/main/proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = 'exclude' // Gradle 10 compatible syntax
}

bootJar { enabled = false }